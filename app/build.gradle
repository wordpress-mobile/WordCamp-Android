apply plugin: 'com.android.application'

repositories{
    maven {
        url 'http://dl.bintray.com/amulyakhare/maven'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.wordcamp"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0-beta-1"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CENTRAL_WORDCAMP_URL", '"' + project.properties["default.central_wordcamp_url"] + '"'
        }

        debug {
            debuggable true
            buildConfigField "String", "CENTRAL_WORDCAMP_URL", '"' + project.properties["default.central_wordcamp_url"] + '"'
        }

        localdebug.initWith(buildTypes.debug);
        localdebug {
            buildConfigField "String", "CENTRAL_WORDCAMP_URL", '"' + project.properties["local.central_wordcamp_url"] + '"'
        }
    }
}

dependencies {
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.loopj.android:android-async-http:1.4.7'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'se.emilsjolander:stickylistheaders:2.6.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.parse.bolts:bolts-android:1.2.0'
}

android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("wc.")) {
            buildType.buildConfigField "String", property.key.replace("wc.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
    }
}

// For app signing release build type
if (["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0) {
    android {
        signingConfigs {
            release {
                storeFile = file(project.storeFile)
                storePassword = project.storePassword
                keyAlias = project.keyAlias
                keyPassword = project.keyPassword
            }
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
