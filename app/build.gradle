 apply plugin: 'com.android.application'

repositories{
    maven {
        url 'http://dl.bintray.com/amulyakhare/maven'
    }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

ext.supportLibrariesVersion = '27.0.1'

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 27
    buildToolsVersion "27.0.1"

    defaultConfig {
        applicationId "org.wordcamp.android"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 8
        versionName "1.1"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CENTRAL_WORDCAMP_URL", '"' + project.properties["default.central_wordcamp_url"] + '"'
        }

        debug {
            debuggable true
            buildConfigField "String", "CENTRAL_WORDCAMP_URL", '"' + project.properties["default.central_wordcamp_url"] + '"'
        }

        localdebug.initWith(buildTypes.debug);
        localdebug {
            buildConfigField "String", "CENTRAL_WORDCAMP_URL", '"' + project.properties["local.central_wordcamp_url"] + '"'
        }
    }
}

dependencies {
    compile "com.android.support:design:$supportLibrariesVersion"
    compile "com.android.support:appcompat-v7:$supportLibrariesVersion"
    compile "com.android.support:cardview-v7:$supportLibrariesVersion"
    compile "com.android.support:recyclerview-v7:$supportLibrariesVersion"
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'se.emilsjolander:stickylistheaders:2.6.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.parse.bolts:bolts-android:1.2.0'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.mcxiaoke.volley:library:1.0.19'
}

android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("wc.")) {
            buildType.buildConfigField "String", property.key.replace("wc.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
    }
}

// For app signing release build type
if (["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0) {
    android {
        signingConfigs {
            release {
                storeFile = file(project.storeFile)
                storePassword = project.storePassword
                keyAlias = project.keyAlias
                keyPassword = project.keyPassword
            }
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
